plugins {
    id 'java'
    id "com.jfrog.bintray" version "1.8.4"
    id "maven-publish"
    id 'org.jetbrains.kotlin.jvm' version '1.3.41'
}

group 'com.github.johnnyjayjay'
version '2.1.3'

sourceCompatibility = 1.8

repositories {
    maven { url "https://hub.spigotmc.org/nexus/content/repositories/snapshots/" }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots" }
    mavenCentral()
}

dependencies {
    api "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    compileOnly "org.spigotmc:spigot-api:1.14.2-R0.1-SNAPSHOT"
}

compileTestKotlin {
    kotlinOptions.jvmTarget = "1.8"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

javadoc.failOnError = false
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier "javadoc"
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

bintray {
    user = System.getenv("BINTRAY_USER")
    key = System.getenv("BINTRAY_KEY")
    publications = ["Spiglin"]
    pkg {
        version {
            name = project.version
            released = new Date()
        }
        repo = "spiglin"
        name = "spiglin"
        vcsUrl = "https://github.com/johnnyjayjay/spiglin.git"
    }
    publish = true
}

def pomConfig = {
    licenses {
        license {
            name "GNU Lesser General Public License, Version 3"
            url "https://www.gnu.org/licenses/lgpl-3.0.html"
        }
    }
    developers {
        developer {
            id "johnnyjayjay"
            email "johnnyjayjay02@gmail.com"
        }
    }
    scm {
        url "https://github.com/johnnyjayjay/spiglin"
    }
}

publishing {
    publications {
        Spiglin(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
            groupId "com.github.johnnyjayjay"
            artifactId "spiglin"
            version version.toString()
            pom.withXml {
                def root = asNode()
                root.appendNode("name", "spiglin")
                root.appendNode("description",
                        "A collection of Kotlin extensions and utilities for the Spigot/Bukkit framework.")
                root.appendNode("url", "https://github.com/johnnyjayjay/spiglin/")
                root.children().last() + pomConfig
            }
        }
    }
}